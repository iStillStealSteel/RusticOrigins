buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url = 'https://lss233.littleservice.cn/repositories/minecraft/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
        classpath 'org.parchmentmc:librarian:1.+'
    }
}
plugins {
    id "me.modmuss50.mod-publish-plugin" version "0.3.5"
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

mixin {
    add sourceSets.main, 'origins_rustic.refmap.json'
}

version = project.mod_version
group = project.maven_group
archivesBaseName = project.archives_base_name

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {

    mappings channel: "parchment", version: "${project.mappings_version}"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            args '-mixin.debug=true', '-mixin.debug.export=true', '-mixin.config=origins_rustic.mixins.json'

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

            mods {
                origins_rustic {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            args '-mixin.debug=true', '-mixin.debug.export=true', '-mixin.config=origins_rustic.mixins.json'

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

            mods {
                origins_rustic {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

            args '--mod', 'origins_rustic', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                origins_rustic {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources {
    srcDirs += 'src/generated/resources'
}

repositories {
    flatDir {
        dir 'libs'
    }
    maven {
        name 'Progwml6 Maven'
        url 'https://dvs1.progwml6.com/files/maven'
    }
    maven {
        name 'Theillusivec4 Maven'
        url 'https://maven.theillusivec4.top'
    }
    maven {
        name 'Curse Maven'
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    implementation  fg.deobf("top.theillusivec4.caelus:caelus-forge:${mc_version}-${caelus_version}")
    implementation  fg.deobf("io.github.edwinmindcraft:calio-forge:${mc_version}-${calio_version}")
    implementation  fg.deobf("io.github.edwinmindcraft:apoli-forge:${mc_version}-${apoli_version}")
    implementation  fg.deobf("io.github.edwinmindcraft:origins-forge:${mc_version}-${origins_version}")

    implementation  fg.deobf("curse.maven:farmers-delight-398521:${farmersdelight_file}")
    implementation  fg.deobf("curse.maven:appleskin-248787:${appleskin_file}")
    compileOnly     fg.deobf("curse.maven:placebo-283644:${placebo_file}")
    compileOnly     fg.deobf("curse.maven:apotheosis-313970:${apotheosis_file}")
    compileOnly     fg.deobf("curse.maven:mutil-351914:${mutil_file}")
    compileOnly     fg.deobf("curse.maven:tetra-289712:${tetra_file}")
    runtimeOnly     fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}")

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

processResources {
    inputs.property "version", version

    filesMatching("META-INF/mods.toml") {
        expand 'version': version,
               'mc_requirements': mc_requirements,
               'forge_requirements': forge_requirements,
               'origins_requirements': origins_requirements
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_author,
                "Specification-Version"   : "1",
                "Implementation-Title"    : mod_id,
                "Implementation-Version"  : mod_version,
                "Implementation-Vendor"   : mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : 'origins_rustic.mixins.json'
        ])
    }
}

jar.finalizedBy('reobfJar')

publishMods {
    file = tasks.jar.archiveFile
    changelog = new File("changelog/${mod_version}.md").text
    version = mod_version
    type = STABLE
    displayName = "Origins Classes Forge ${mod_version}"
    modLoaders.add("forge")

    curseforge {
        projectId = "610127"
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        minecraftVersions.add(mc_version)

        requires {
            slug = "origins-forge"
        }
    }

    modrinth {
        projectId = "HTjxjxqK"
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        minecraftVersions.add(mc_version)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${projectDir}/mcmodsrepo"
        }
    }
}
